#! perl
use strict;
use warnings;
use File::Basename;
use Perl::PrereqInstaller;
use Getopt::Long;

my ( $help, $dry_run, $version );

my $options = GetOptions(
    "help"    => \$help,
    "dry-run" => \$dry_run,
    "version" => \$version,
);

my ( $dir, $pattern ) = @ARGV;

my $script_path = $0;
my $script_name = fileparse $script_path;

if ( defined $version ) {
    die "$script_name v$Perl::PrereqInstaller::VERSION\n";
}

my $usage = <<EOF;
usage: $script_name DIRECTORY [REGEX-PATTERN]
    -h, --help
    -d, --dry-run
    -v, --version
EOF

die $usage if defined $help;
die $usage unless defined $dir;

my $installer = Perl::PrereqInstaller->new;
$installer->check_modules_deep( $dir, $pattern );

my @scan_errors = $installer->scan_errors;

if ( scalar @scan_errors > 0 ) {
    print "File parsing errors:\n";
    print "  $_\n" for @scan_errors;
}

my @missing = $installer->not_installed;

if ( scalar @missing > 0 ) {
    print "\n";
    print "Modules to install:\n";
    print "  $_\n" for @missing;
    print "\n";

    exit if $dry_run;

    $installer->cpanm;
    print "\n";

    print "Successfully installed:\n";
    print "  $_\n" for $installer->newly_installed;
    print "\n";

    print "Failed to install:\n";
    print "  $_\n" for $installer->failed_install;
    print "\n";
}
else {
    print "No missing modules need to be installed!\n";
}

exit;
