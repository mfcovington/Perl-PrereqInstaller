#! perl
use strict;
use warnings;
use File::Basename;
use Module::Extract::Install;
use Getopt::Long;

my ( $help, $version );

my $options = GetOptions(
    "help"    => \$help,
    "version" => \$version,
);

my @files = @ARGV;

my $script_path = $0;
my $script_name = fileparse $script_path;

if ( defined $version ) {
    die "$script_name v$Module::Extract::Install::VERSION\n";
}

my $usage = <<EOF;
usage: $script_name PERL-FILE1 [PERL-FILE2 ...]
    -h, --help
    -v, --version
EOF

die $usage if defined $help;
die $usage if scalar @files == 0;

my $installer = Module::Extract::Install->new;
$installer->check_modules(@files);

my @missing = $installer->not_installed;

if ( scalar @missing > 0 ) {
    print "\n";
    print "Modules to install:\n";
    print "  $_\n" for @missing;
    print "\n";

    $installer->cpanm;
    print "\n";

    print "Successfully installed:\n";
    print "  $_\n" for $installer->newly_installed;
    print "\n";

    print "Failed to install:\n";
    print "  $_\n" for $installer->failed_install;
    print "\n";
}
else {
    print "No missing modules need to be installed!\n";
}

exit;
